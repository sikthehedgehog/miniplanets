;****************************************************************************
; InitSound
; Initializes the sound engine.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

InitSound:
    jsr     (Sona_Init).w               ; Boot up sound engine
    
    clr.b   (BGMToPlay)                 ; Playing nothing yet
    clr.b   (SFXToPlay)
    clr.b   (CurrentBGM)
    clr.b   (CurrentSFX)
    
    rts                                 ; End of subroutine

;****************************************************************************
; UpdateSound
; Updates sound output.
;----------------------------------------------------------------------------
; breaks: d5-d7, a4-a6
;****************************************************************************

UpdateSound:
    movem.l d0-d1/a0-a1, -(sp)          ; Save registers (blame Sona's API)
    
    moveq   #0, d7                      ; Check if a BGM should play
    move.b  (BGMToPlay), d7
    beq.s   @NoBGM
    bmi.s   @StopBGM
    
    clr.b   (BGMToPlay)
    subq.w  #1, d7                      ; Play the BGM then
    add.w   d7, d7
    add.w   d7, d7
    lea     (BGMList), a6
    move.l  (a6,d7.w), a0
    lea     (InstrumentList), a1
    moveq   #NUM_INSTRUMENTS, d0
    jsr     (Sona_PlayBGM).w
    bra.s   @Done
    
@StopBGM:
    clr.b   (BGMToPlay)                 ; Stop BGM instead?
    jsr     (Sona_StopAllSound).w
    bra.s   @Done
    
@NoBGM:
    
    moveq   #0, d7                      ; Check if a SFX should play
    move.b  (SFXToPlay), d7
    beq.s   @NoSFX
    clr.b   (SFXToPlay)
    move.b  d7, (CurrentSFX)
    
    subq.w  #1, d7                      ; Play the SFX then
    add.w   d7, d7
    add.w   d7, d7
    lea     (SFXList), a6
    move.l  (a6,d7.w), a0
    lea     (InstrumentList), a1
    moveq   #NUM_INSTRUMENTS, d0
    move.b  d7, d1
    jsr     (Sona_PlaySfx).w
    bra.s   @Done
@NoSFX:
    
    jsr     (Sona_GetStatus).w          ; Check if a SFX is playing yet
    btst.l  #SONA_STATUS_SFX1, d0
    bne.s   @SFXPlaying
    clr.b   (CurrentSFX)
@SFXPlaying:
    
@Done:
    movem.l (sp)+, d0-d1/a0-a1          ; Restore registers
    rts                                 ; End of subroutine

;****************************************************************************
; PlayBGM
; Plays background music.
;----------------------------------------------------------------------------
; input d7.b ... BGM ID
;****************************************************************************

PlayBGM:
    cmp.b   (CurrentBGM), d7            ; Playing this BGM already?
    bne.s   @NotTheSame
    rts
@NotTheSame:
    
    move.b  d7, (BGMToPlay)             ; Schedule BGM to play
    move.b  d7, (CurrentBGM)
    rts                                 ; End of subroutine

;****************************************************************************
; StopBGM
; Stops background music.
;----------------------------------------------------------------------------
; input d7.b ... BGM ID
;****************************************************************************

StopBGM:
    move.b  #-1, (BGMToPlay)
    move.b  #0, (CurrentBGM)
    rts

;****************************************************************************
; PlaySFX
; Plays a sound effect.
;----------------------------------------------------------------------------
; input d7.b ... SFX ID
;****************************************************************************

PlaySFX:
    cmp.b   (CurrentSFX), d7            ; Make sure no higher priority SFX
    blo.s   @Overriden                    ; is already playing
    
    move.b  d7, (CurrentSFX)            ; Schedule SFX to play
    move.b  d7, (SFXToPlay)
    
@Overriden:
    rts                                 ; End of subroutine
